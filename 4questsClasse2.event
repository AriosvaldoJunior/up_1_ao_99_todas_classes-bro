#From- Macro Quest Arruaceiro 2.0

#By Gaspel
#Edited- by CALF
#convertido e reescrito pra eventMacro por Nipodemos
automacro virarArruaceiroInicio_salvarEmMorocc {
	JobID 6 #gatuno
	JobLevel = 50
	FreeSkillPoints = 0
	priority 2
	exclusive 1
	ConfigKeyNot saveMap morocc
	ConfigKeyNot In_saveMap_sequence true
	ConfigKeyNot quest_eden em_curso
	ConfigKeyNot quest_eden terminando
	NpcNotNear /Arruaceira da Guilda/
	QuestInactive 2017
	QuestInactive 2018
	QuestInactive 2022
	QuestInactive 2023
	QuestInactive 2024
	QuestInactive 2026
	call SetSaveIn "morocc"
}

automacro virarArruaceiroInicio_IrNoNpc {
	JobID 6 #gatuno
	JobLevel = 50
	FreeSkillPoints = 0
	priority 2
	exclusive 1
	ConfigKey saveMap morocc
	NpcNotNear /Arruaceira da Guilda/
	QuestInactive 2017
	QuestInactive 2018
	QuestInactive 2022
	QuestInactive 2023
	QuestInactive 2024
	QuestInactive 2026
	call {
		call pararDeAtacar
		do conf lockMap none
		do conf -f virarArruaceiro true
		do move in_rogue &rand(361,366) &rand(112,117)
	}
}

automacro virarArruaceiroInicio {
	JobID 6 #gatuno
	JobLevel = 50
	priority 2
	exclusive 1
	NpcNear /Arruaceira da Guilda/
	QuestInactive 2017
	QuestInactive 2018
	QuestInactive 2022
	QuestInactive 2023
	QuestInactive 2024
	QuestInactive 2026
	call {
		#do talknpc 363 122 c r0 r0 r0 r2 r3 r1 r3 r2 r1 r0 r1 c c c #Arruaceira da Guilda
		do talk $.NpcNearLastBinId
		do talk resp 0
		do talk resp 0
		do talk resp 0
		do talk resp 2
		do talk resp 3
		do talk resp 1
		do talk resp 3
		do talk resp 2
		do talk resp 1
		do talk resp 0
		do talk resp 1
	}
}

automacro virarArruaceiro_etapa2_FalarComSmith {
	JobID 6 #gatuno
	QuestActive 2017
	exclusive 1
	run-once 1
	call {
		do move in_rogue &rand(370,373) &rand(20,24)
		do talk &npc ( 376 23 ) #Smith
	}
}


automacro virarArruaceiro_etapa3_coletarItens {
	JobID 6 #gatuno
	QuestActive 2018
	exclusive 1
	ConfigKeyNot questArruaceiro peguei
	timeout 100
	call  {
		do iconf 510 7 1 0 #ervaAzul
		do iconf 957 10 1 0 #unhaApodrecida
		do iconf 932 10 1 0 #osso
		do iconf 958 10 1 0 #mandibula
		$ervaAzul       = &invamount (Erva Azul)
		$unhaApodrecida = &invamount (Unha Apodrecida)
		$osso           = &invamount (Osso)
		$mandibula      = &invamount (Mandíbula Horrenda)
		if ( $ervaAzul < 6 ) goto Erva
		if ( $ervaAzul >= 6 && $unhaApodrecida < 10 ) goto Unha
		if ( $ervaAzul >= 6 && $unhaApodrecida >= 10 && $osso < 10 ) goto Osso
		if ( $ervaAzul >= 6 && $unhaApodrecida >= 10 && $osso >= 10 && $mandibula < 10 ) goto Mandibula


		:Erva
		log Pegar Erva
		[
		do conf lockMap cmd_fild02
		do mconf 1266 0 0 0
		do mconf 1073 0 0 0
		do mconf 1254 0 0 0
		do mconf Cornutus 0 0 0
		do mconf 1074 0 0 0
		do mconf 1391 0 0 0
		do conf attackAuto_inLockOnly 1
		call voltarAtacar
		log =================================
		log Estou coletando ERVA AZUL pra
		log quest de arruaceiro
		log =================================
		]
		stop


		:Unha
		log Pegar Unha
		[
		do conf lockMap pay_dun00
		do mconf 1005 0 0 0
		do mconf 1031 0 0 0
		do mconf 1078 0 0 0
		do mconf 1084 0 0 0
		do conf attackAuto_inLockOnly 1
		call voltarAtacar
		log ================================
		log Estou coletando UNHA APODRECIDA
		log pra quest de arruaceiro
		log ================================
		]
		stop


		:Osso
		log Pegar Osso
		[
		do conf lockMap pay_dun01
		do mconf Drainliar 0 0 0
		do mconf Eggyra 0 0 0
		do mconf 1084 0 0 0
		do mconf 1078 0 0 0
		do conf attackAuto_inLockOnly 1
		do conf route_randomWalk 1
		call voltarAtacar
		log ==============================
		log Estou coletando OSSO para
		log a quest de arruaceiro
		log ==============================
		]
		stop


		:Mandibula
		log Pegar mandibula
		[
		do conf lockMap pay_dun00
		do mconf 1005 0 0 0
		do mconf 1031 1 0 0
		do mconf 1078 0 0 0
		do mconf 1084 0 0 0
		do mconf 1076 0 0 0
		do conf attackAuto_inLockOnly 1
		do conf route_randomWalk 1
		call voltarAtacar
		log ==============================================
		log Estou coletando MANDIBULA pra quest de arruaceiro
		log Recomendado comprar esse item em vez de dropar
		log Tempo pra dropar todas as mandíbulas: 12 horas
		log ==============================================
		]
		stop
	}
}

automacro virarArruaceiro_etapa3_tenhoOsItensIrProNpc {
	InInventory "Erva Azul" >= 6
	InInventory "Unha Apodrecida" >= 10
	InInventory "Osso" >= 10
	InInventory "Mandíbula Horrenda" >= 10
	QuestActive 2018
	Zeny > 9999
	NpcNotNear /Smith/
	exclusive 1
	call {
		lock virarArruaceiro3
		do conf -f questArruaceiro peguei
		log Vamos No NPC já temos os Itens...
		call pararDeAtacar
		do conf lockMap none
		do conf attackAuto_inLockOnly 0
		do mconf 1266 1 0 0
		do mconf 1073 1 0 0
		do mconf 1254 1 0 0
		do mconf Cornutus 1 0 0
		do mconf 1074 1 0 0
		do mconf 1391 1 0 0
		do mconf 1005 1 0 0
		do mconf 1031 1 0 0
		do mconf 1078 1 0 0
		do mconf 1084 1 0 0
		do mconf 1076 1 0 0
		lock virarArruaceiro3
		do move in_rogue &rand(370,373) &rand(20,24)
	}
}

automacro virarArruaceiro_etapa3_tenhoOsItensbugged {
	InInventory "Erva Azul" >= 6
	InInventory "Unha Apodrecida" >= 10
	InInventory "Osso" >= 10
	InInventory "Mandíbula Horrenda" >= 10
	QuestActive 2018
	Zeny < 10000
	exclusive 1
	call {
		log por algum motivo, o bot não tem nem os 10k de zeny pra fazer a quest
		log tentando resolver isso
		do autosell
	}
}


automacro virarArruaceiro_etapa3_tenhoOsItens {
	InInventory "Erva Azul" >= 6
	InInventory "Unha Apodrecida" >= 10
	InInventory "Osso" >= 10
	NpcNear /Smith/
	InInventory "Mandíbula Horrenda" >= 10
	Zeny > 9999
	QuestActive 2018
	exclusive 1
	call {
		lock virarArruaceiro3
		do conf lockMap none
        do conf sitAuto_hp_upper 80
		call pararDeAtacar
		do talk $.NpcNearLastBinId #Smith
		do talk $.NpcNearLastBinId #Smith
		do talk $.NpcNearLastBinId #Smith
		lock virarArruaceiro3
	}
}

automacro virarArruaceiro_etapa4 {
	JobID 6 #gatuno
	exclusive 1
	JobLevel = 50
    CurrentHP >= 80%
	priority 5 #prioridade baixa
	delay 5
	QuestActive 2022, 2023, 2024
	call {
		if ($.map = in_rogue && $.pos ~ 169 34, 246 25, 164 106) {
		    [
		    log ==================================
			log essa macro está se ativando no momento errado
			log reporte esse bug ao criador da macro
			log ==================================
			]
			stop
		}
		do conf lockMap none
        do stand
		do conf attackAuto -1
        log sua posição atual é: $.map $.pos
		switch ($.QuestActiveLastID) {
			case (= 2022) { #opção 1
				do conf -f questArruaceiro 1
				call irAteLocal_questArruaceiro "cmd_fild09 107 195" "1" "2" "1" "0" #local e respostas apropriadas
			}
			case (= 2023) { #opção 2
				do conf -f questArruaceiro 2
				call irAteLocal_questArruaceiro "cmd_fild04 304 180" "2" "1" "1" "2" #local e respostas apropriadas
			}
			case (= 2024) { #opção 3
				do conf -f questArruaceiro 3
				call irAteLocal_questArruaceiro "cmd_fild09 335 143" "0" "2" "4" "0" #local e respostas apropriadas
			}
		}
	}
}

macro irAteLocal_questArruaceiro {
    [
    log ================================
    log =Mapa pra qual está se movendo: '$.param[0]'
    log =Sequência de conversação: '$.param[1] $.param[2] $.param[3] $.param[4]'
    log ================================
    ]
    ## $.param[0] tem como valor cidade e coordenadas
    ## $.param[1] até $.param[4] tem como valor as respostas corretas pra entrar na casa

	do move $.param[0]
	do talk resp $.param[1]
	do talk resp $.param[2]
	do talk resp $.param[3]
	do talk resp $.param[4]
	set exclusive 0
	pause 5
}

automacro virarArruaceiro_etapa5_DentroDaCasa {
	JobLevel = 50
	IsInMapAndCoordinate in_rogue 169 34, in_rogue 246 25, in_rogue 164 106
	exclusive 1
	priority -5
	call {
		switch ($.pos) {
			case (= 169 34) {
				do talknpc 160 34 c r0 c
			}
			case (= 246 25) {
				do move in_rogue 244 33
				do talknpc 244 39 c r0 c
			}
			case (= 164 106) {
				do move in_rogue 172 108
				do talknpc 177 109 c r0 c
			}
		}
	}
}

automacro virarArruaceiro_etapa6_labirinto {
	JobLevel = 50
	exclusive 1
	IsInMapAndCoordinate in_rogue 15 105
	macro_delay 0.5
	call {
		#ahhhh o labirinto.... o bixin vai morrer algumas vezes aqui viu... ou não kkkk
		do conf attackAuto -1
		do conf itemsTakeAuto 0
		do conf lockMap none
		log ================================
		log Iniciando Labirinto
		log ================================
		do move 23 105
		do move 23 334
		#preguiça de fazer o resto das coordenadas específicas
		do move in_rogue 359 117
	}
}

automacro virarArruaceiro_etapa6_morreuNoLabirinto_hpAlto {
	QuestActive 2026
	ConfigKeyNot questArruaceiro none
	JobLevel = 50
	NotInMap in_rogue
	CurrentHP >= 80%
	exclusive 1
	call {
		do stand
		[
		log ==================================================
		log Já esou com hp alto, vou retentar o labirinto
		log ==================================================
		]
		do conf lockMap none
		log a opção de caminho é a &config(questArruaceiro)
		# se vc tiver morrido no labirindo, essa macro que vai fazer voce ir la tentar denovo
		# vai pegar qual o caminho certo e seguir ele
		switch ( &config(questArruaceiro) ) {
			case (= 1) call irAteLocal_questArruaceiro "cmd_fild09 107 195" "1" "2" "1" "0"  #local e respostas apropriadas
			case (= 2) call irAteLocal_questArruaceiro "cmd_fild04 304 180" "2" "1" "1" "2"  #local e respostas apropriadas
			case (= 3) call irAteLocal_questArruaceiro "cmd_fild09 335 143" "0" "2" "4" "0"  #local e respostas apropriadas
			else {
				[
				log ================================
				log =a macro deveria ter escolhido um caminho, mas não conseguiu, isso é um bug
				log ================================
				]
				stop
			}
		}
		log Indo até o local &config(questArruaceiro)
	}
}

automacro virarArruaceiro_etapa6_morreuNoLabirinto_hpBaixo {
	QuestActive 2026
	JobLevel = 50
	NotInMap in_rogue
	CurrentHP < 80%
	exclusive 1
	timeout 120
	call {
		[
		log ====================================
		log MORRI TENTANDO PASSAR PELO LABIRINTO
		log HP ATUAL: $.CurrentHPLastPercent%
		log ESPERANDO HP FICAR ACIMA DE 80%
		log ====================================
		]
		do sit
		if ( &config (sitAuto_hp_upper) != 80 ) {
		do conf sitAuto_hp_lower 20
		do conf sitAuto_hp_upper 80
		}
	}
}

automacro virarArruaceiro_etapaFinal {
	JobLevel = 50
	#IsInMapAndCoordinate in_rogue 359 117
	NpcNear /Marybell/
	InMap in_rogue
	QuestActive 2026
	exclusive 1
	call {
		log ===========================
		log PASSEI PELO LABIRINTO!!!!
		log ===========================
		do move in_rogue 369 119
		do talknpc 363 122 c c c
		do conf itemsTakeAuto 1
	}
}

automacro virarArruaceiro_etapaFinal_Alternativo {
	JobLevel = 50
	#IsInMapAndCoordinate in_rogue 378 113
	NpcNear /Homem Assustador/
	InMap in_rogue
	QuestActive 2026
	exclusive 1
	call {
		log ===========================
		log PASSEI PELO LABIRINTO!!!!
		log ===========================
		do move in_rogue 369 119
		do talknpc 363 122 c c c
		do conf itemsTakeAuto 1
	}
}

automacro jaSouArruaceiro {
	JobID 17 #arruaceiro
	ConfigKey virarArruaceiro true
	exclusive 1
	call {
		do conf teleportAuto_MaxDmg 1000
		do iconf Erva Azul 0 1 0
		do iconf Mandíbula Horrenda 0 0 1
		do iconf Osso 0 0 1
		do iconf Unha Apodrecida 0 0 1
		$check = GetIndexAndEquipped("robe", 2560) #Capa Valhalla
		if ($check != -1) do eq $check
		$check = GetIndexAndEquipped("topHead", 5583) #Chapeu valhalla
		if ($check != -1) do eq $check
		call pararDeAtacar
		do conf virarArruaceiro none
		do conf questArruaceiro none
	}
}
#
#
#
# FIM DA QUEST PRA VIRAR ARRUACEIRO
#


#################################################################
#																#
#   					Virando Alquimista						#
#																#
#################################################################

#################################################################
#																#
# 		Distribuindo os Pontos de Habilidade caso não			#
#					tenham sido distribuídos					#
#																#
#################################################################

automacro virarAlquimistaInicio_Bugged {
	FreeSkillPoints > 0
	timeout 30
	JobLevel = 50
	JobID 5 #Mercador
	exclusive 1
	call {
		[
		log =============================================
		log = Estou pronto para virar Alquimista, porém =
		log =     os pontos não estão distribuídos...   =
		log =            Vamos resolver isso!			=
		log =============================================
		]
		do conf skillsAddAuto 1
		do conf skillsAddAuto_list NV_BASIC 9, MC_INCCARRY 10, MC_DISCOUNT 9, MC_OVERCHARGE 10, MC_PUSHCART 10, MC_VENDING 10, MC_IDENTIFY 1, AM_BIOETHICS 1, AM_REST 1, AM_CALLHOMUN 1, AM_RESURRECTHOMUN 5, AM_AXEMASTERY 10, AM_LEARNINGPOTION 10, AM_PHARMACY 10, AM_POTIONPITCHER 5, AM_CANNIBALIZE 5, AM_SPHEREMINE 3
		[
		log ================================================================
		log = Se isso estiver aparecendo repetidamente, a macro tem um bug =
		log ================================================================
		]
	}
}

automacro virarAlquimista_noMeioDaQuest_bugged {
	ConfigKeyNot questAlquimista none
	JobLevel = 50
    JobID 5 #Mercador
    ConfigKeyNot virarAlquimista true
    exclusive 1
    call {
 		#essa automacro só se ativará, se a gente ta tiver fazendo a quest
        #mas a config virarAlquimista nao tiver true
        #é essencial que ela esteja true nesse momento, senão
        #a macro de up vai passar por cima e bugar a treta
 		do conf -f virarAlquimista true
    }
}
#################################################################
#																#
# 				Indo para a Guilda dos Alquimistas				#
#																#
#################################################################

automacro virarAlquimistaInicio_IrNoNpc {
    JobID 5 #Mercador
	JobLevel = 50
	FreeSkillPoints = 0
	priority 2
	exclusive 1
    ConfigKeyNot virarAlquimista true
	NpcNotNear /Alquimista sênior/
	call {
		call pararDeAtacar
		do conf lockMap none
		do move alde_alche &rand(25,27) &rand(179,181)
	}
}

#################################################################
#																#
# 			Começando a Quest de Alquimista!					#
#		Parte 1 - Falando com o Alquimista Sênior				#
#																#
#################################################################

automacro virarAlquimistaInicio {
	JobID 5 #Mercador
	JobLevel = 50
	priority 2
    timeout 30
    ConfigKeyNot virarAlquimista true
	NpcNear /Alquimista sênior/
	call {
		do conf autoTalkCont 1
		do talk $.NpcNearLastBinId
		do talk resp 1
		do talk resp 0
		do talk resp 0
	}
}

#############################################################################
#																			#
# 					 Continuando a Quest de Alquimista!						#
#		Parte 2 - Identificando/Comprando/Levando os itens necessários 		#
#					     para o Alquimista Sênior							#
#																			#
#############################################################################

automacro virarAlquimista_descobrindoQualItem {
	NpcMsg /Como eu te disse, você precisa trazer/
    exclusive 1
    call {
    	do conf -f virarAlquimista true
    	[
        log =*=*=*=*=*=*=*=*=*=*=*=*=*=*==*=*=*=
    	log a mensagem é: $.NpcMsgLastMsg
        log *=*=*=*=*=*=*=*=*=*=*=*==*=*=*=*=*=*
        ]
        $mensagemExtraida = extrairMensagem("$.NpcMsgLastMsg")
    	if ( $mensagemExtraida =~ /F.ria Selvagem/) {
        	do conf -f questAlquimista comprarPocao
        } elsif ( $mensagemExtraida =~ /Flecha/ ) {
        	do conf -f questAlquimista comprarFlecha
        } elsif ( $mensagemExtraida =~ /Fornalha/ ) {
        	do conf -f questAlquimista comprarFornalha
        } else {
        	log erro configurado proximo passo, chequee a macro
        }
    }
}

sub extrairMensagem {
	my ($mensagem) = @_;
    if ($mensagem =~ /trazer (.+) para completar/) {
    	return $1;
    }
    else {
    	warning "ERRO, NÃO FOI POSSIVEL EXTRAIR MENSAGEM\n";
    	return -1;
    }
}

automacro virarAlquimista_comprarFornalha {
	exclusive 1
	JobID 5 #Mercador
	JobLevel = 50
	ConfigKey questAlquimista comprarFornalha
    InInventoryID 612 < 100 #Mini-Fornalha
    call {
    	do move geffen
        do move geffen_in 106 178
        do talk &npc(110 172)
        do talk resp 1
        do talk resp 0
        do talk num 100
    }
}

automacro virarAlquimista_comprarFuria {
	exclusive 1
	JobID 5 #Mercador
	JobLevel = 50
	InInventoryID 657 < 7 #Poção da Fúria Selvagem
    ConfigKey questAlquimista comprarPocao
    call comprarItem "cmd_in01" "79 182" "Ferramentas" "Poção da Fúria Selvagem" "7"
}

automacro virarAlquimista_comprarFlechaDeFogo {
	exclusive 1
	JobID 5 #Mercador
	JobLevel = 50
	ConfigKey questAlquimista comprarFlecha
    InInventoryID 1752 <= 500 #Flecha de Fogo
    call comprarItem "alb2trea" "87 65" "Ferramentas" "Flecha de Fogo" "500"
}

macro comprarItem {
	### $.param[0] tem como valor a cidade
    ### $.param[1] tem como valor as coordenadas
    ### $.param[2] tem como valor o NPC
    ### $.param[3] tem como valor o item
    ### $.param[4] tem como valor a quantidade
	do move $.param[0] $.param[1] &rand(1,6)
    do talk &npc(/$.param[2]/)
    do store
    do buy &store($.param[3]) $.param[4]
}

automacro virarAlquimista_FuriaComprada_indoEntregar {
	InInventoryID 657 >= 7 #Poção da Fúria Selvagem
	ConfigKey questAlquimista comprarPocao
	exclusive 1
    call voltarProAlquimistaSenior
}

automacro virarAlquimsta_FornalhaComprada_indoEntregar {
    InInventoryID 612 >= 100 #Mini-Fornalha
	ConfigKey questAlquimista comprarFornalha
	exclusive 1
    call voltarProAlquimistaSenior
}

automacro virarAlquimista_FlechaComprada_indoEntregar {
    InInventoryID 1752 >= 500 #Flecha de Fogo
	ConfigKey questAlquimista comprarFlecha
	exclusive 1
   	call voltarProAlquimistaSenior
}

macro voltarProAlquimistaSenior {
	do move alde_alche &rand(25,27) &rand(178,180)
    do talk &npc (/Alquimista sênior/)
    do conf lockMap none
    pause 3
    
    #isso vai cheecar se realmente entregamos o item, ja que está sempre suscetivel a falhas
    #se não tiver entregado por qualquer motivo que seja, ele simplesmente vai tentar denovo 
    #Se conseguir, ele move pra o proximo passo
    if ($.InInventoryIDLast = 657) { #Poção da Fúria Selvagem
    	if (&inventory(Poção da Fúria Selvagem) < 7) do conf -f questAlquimista questionario
    } elsif ($.InInventoryIDLast = 612) { #Mini-Fornalha
    	if (&inventory(Mini-Fornalha) < 100) do conf -f questAlquimista questionario
    } elsif ($.InInventoryIDLast = 1752) { #Flecha de Fogo
    	if (&inventory(Flecha de Fogo)  < 500) do conf -f questAlquimista questionario
    }
}		

#################################################################
#																#
# 			    Continuando a Quest de Alquimista!				#
#				 Parte 3 - Falando com Raspuchin				#
#																#
#################################################################

automacro virarAlquistia_npcDoQuestionário {
	ConfigKey questAlquimista questionario
    timeout 60
    call {
    	[
		log ======================================================
		log = 			Como estamos no nível de classe 50,		 =
        log =  não precisamos responder o questionário, somente  =
		log = 				falar com ele! hehehe				 =
		log ======================================================
		]
        do move alde_alche &rand(166,168) &rand(106,108)
        pause 5
		do talk &npc (/Alquimista Misterioso/)
    }
}

automacro virarAlquimista_npcQuestionario_bugged {
	timeout 30
    NpcMsgName /vez que falou comigo/ / Misterioso/
    ConfigKey questAlquimista questionario
    call {
    	do conf -f questAlquimista npcDaFlor
    }
}

automacro virarAlquimista_jaFaleiComNpcQuestionario {
  	exclusive 1
    NpcMsg /Diga a Darcya que eu te mandei/
	ConfigKey questAlquimista questionario
    call {
        do conf -f questAlquimista npcDaFlor
    }
}
    	
#################################################################
#																#
# 			   Continuando a Quest de Alquimista!				#
#				 Parte IV - Falando com Darcya					#
#																#
#################################################################

automacro virarAlquimista_NpcDaFlor {
	ConfigKey questAlquimista npcDaFlor
    timeout 30
	call {
		call pararDeAtacar
        do conf lockMap none
        do iconf 710 0 0 0 #Flor das Ilusões
		do move alde_alche &rand(13,16) &rand(18,20)
		do talk &npc(/Pesquisador/)
        do talk resp 0
	}
}

automacro virarAlquimista_faleiComNpcDaFlor_bugged {
	ConfigKey questAlquimista npcDaFlor
	NpcMsgName /Volte quando estiver tudo pronto/ /Pesquisador/
	exclusive 1
	call {
		do conf -f questAlquimista pegarAFlor
	}
}
automacro virarAlquimista_faleiComNpcDaFlor {
	ConfigKey questAlquimista npcDaFlor
    exclusive 1
    NpcMsg /Consiga os itens e volte/
    call {
        do conf -f questAlquimista pegarAFlor
    }
}

#################################################################
#																#
# 				Caso não tenha a Flor, vou ter que				#
#			  comprá-la! Configurando BetterShopper...			#
#																#
#################################################################

automacro virarAlquimista_naoTenhoAFlor {
	ConfigKey questAlquimista pegarAFlor
	ConfigKeyNot questAlquimista fimDaQuest
    exclusive 1
    Zeny >= 110000
    InInventoryID 710 < 1 #Flor das Ilusões
    ConfigKeyNot BetterShopper_on 1
    call {
    	call pararDeAtacar
        do conf lockMap none
        if ( recursoTecnicoPaliativo() = 1) { #Significa que o BetterShopper está ativado
        	do conf lockMap prontera
            do conf route_randomWalk 1
            do conf route_randomWalk_inTown 1
            do conf itemsTakeAuto 1
            do conf itemsGatherAuto 0
            do conf itemsMaxWeight_sellOrStore 40
            
            if (&config(BetterShopper_0) != Flor das Ilusões) {
        		configurarBetterShopper()
                pause 3
                do reload config
            }
        } else {
        	[
        	log =*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=
            log *  		É preciso baixar e ativa o plugin BetterShopper.	  *
            log =  		Até lá nem a macro nem o Openkore vão continuar!	  =
            log * Caso já tenha feito isso, feche e abra o Openkore novamente!*
            log =*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=
            ]
            do eval Misc::offlineMode();
            stop
        }
    }
}

automacro virarAlquimista_naoTenhoAFlor_bugged {
	ConfigKey questAlquimista pegarAFlor
	ConfigKeyNot questAlquimista fimDaQuest
	exclusive 1
	Zeny >= 110000
	InInventoryID 710 < 1 #Flor das Ilusões
	ConfigKey BetterShopper_on 1
	NotInMap prontera
	call {
		do move prontera
		do conf lockMap prontera
	}
}

sub configurarBetterShopper {
	open (my $write, '>>:encoding(UTF-8)', Settings::getControlFilename('config.txt'));
    print $write "BetterShopper_on 1\n";
	print $write "BetterShopper Flor das Ilusões {\n";
	print $write "maxPrice 150000\n";
	print $write "maxAmount 1\n";
	print $write "disabled 0\n";
	print $write "}\n";
    close($fh);
}

automacro virarAlquimista_naoTenhoAFlor_NemZeny {
	ConfigKey questAlquimista pegarAFlor
	ConfigKeyNot questAlquimista fimDaQuest
    exclusive 1
    Zeny < 110000
    InInventoryID 710 < 1 #Flor das Ilusões
    ConfigKeyNot lockMap pay_fild09
    ConfigKeyNot In_saveMap_sequence true
    timeout 120
    call {
    	[
    	log =====================================================
        log =Não tenho zeny suficiente para comprar a Flor	 
        log =Então vou conseguir o dinheiro!				 
        log =Começando o processo de vender loot e conseguir mais
        log =====================================================
        ]
        do autosell
        do iconf 690 0 0 1 #Lanças de Fogo Nível 3
        do iconf 757 0 0 1 #Minério de Elunium
        do iconf 907 0 0 1 #Resina
        do iconf 947 0 0 1 #Chifre
        do iconf 914 0 0 1 #Felpa
        do iconf 919 0 0 1 #Couro de Animal
        do iconf 943 0 0 1 ##Casca Rija
        do iconf 935 0 0 1 #Casca
        do iconf 1011 0 0 1 #Emveretarcon
        do iconf 2329 0 0 1 #Armadura de Madeira
        do iconf 1026 0 0 1 #Avelã
        do iconf 2502 0 0 1 #Capuz [1]
        do iconf 2402 0 0 1 #Sandálias [1]
        
        call voltarAtacar
        call configurarMapa "payon" "pay_fild09"
    }
}

sub recursoTecnicoPaliativo {
	my $estaNoSys;
    my $controlfile = Settings::getControlFilename('sys.txt');
	open(FILE, '<:encoding(UTF-8)', $controlfile);
	my @lines = <FILE>;
	close(FILE);
	chomp @lines;
    
	foreach my $line (@lines) {
		next if $line =~ /^$/ || $line =~ /^#/;
		if ($line =~ /BetterShopper/ ) {
			$estaNoSys = 1 ;
            last;
		}
	}
    
    if ($estaNoSys == 1 ) {
    	return 1;
    } else {
    	return -1;
    }
}

automacro virarAlquimista_juntandoZeny {
	ConfigKey questAlquimista pegarAFlor
	ConfigKeyNot questAlquimista fimDaQuest
    exclusive 1
    ConfigKey lockMap pay_fild09
    ConfigKey saveMap payon
    timeout 180
    call {
    	[
        log ======================================
        log =Estou coletando items pra fazer zeny
        log =Para Quest de Alquimista		 
        log ======================================
        ]
        if (&config(attackAuto) != 2) call voltarAtacar
        if (&config(itemsTakeAuto) != 2) do conf itemsTakeAuto 2
        if (&config(itemsGatherAuto) != 2) do conf itemsGatherAuto 2
        if (&config(itemsMaxWeight_sellOrStore) != 30) do conf itemsMaxWeight_sellOrStore 30
    }
}


automacro virarAlquimista_euTenhoAFlor {
	ConfigKey questAlquimista pegarAFlor
	InInventoryID 710 >= 1 #Flor das Ilusões
    timeout 60
	call {
		do conf -f questAlquimista jaPegueiAFlor
	}
}
automacro virarAlquimista_indoEntregaAFlor {
	ConfigKey questAlquimista jaPegueiAFlor
	timeout 60
	InInventoryID 710 >= 1 #Flor das Ilusões
	call {
		call pararDeAtacar
        do conf lockMap none
        do conf route_randomWalk_inTown 0
        do conf BetterShopper_on none
		do move alde_alche &rand(13,16) &rand(18,20)
		do talk &npc(/Pesquisador/)
        do talk resp 1
    }
}

automacro virarAlquimista_entregueiAFlor {
	ConfigKey questAlquimista jaPegueiAFlor
    exclusive 1
    NpcMsgName /torne-se um Alquimista de verdade/ /Pesquisador/
    priority -5 #alto
    call {
    	pause 1
        do conf -f questAlquimista fimDaQuest
    }
}

automacro virarAlquimista_entregueiAFlor_bugged {
	exclusive 1
    NpcMsgName /de um Alquimista/ /Pesquisador/
    ConfigKey questAlquimista jaPegueiAFlor
    call {
    	do conf -f questAlquimista fimDaQuest
    }
}

automacro virarAlquimista_entregueiAFlor_bugged2 {
	exclusive 1
	ConfigKey questAlquimista jaPegueiAFlor
	InInventoryID 710 < 1 #Flor das Ilusões
    call {
    	#se isso ativar, significa que entregamos a flor
    	do conf -f questAlquimista fimDaQuest
    }
}

#################################################################
#																#
# 				Terminando a Quest de Alquimista!				#
#			  Parte V - Falando com Alquimista Chefe			#
#																#
#################################################################

automacro virarAlquimista_FimDaQuest_FalarComAlquimistaChefe {
	ConfigKey questAlquimista fimDaQuest
    JobLevel = 50
    JobID 5
    exclusive 1
    call {
    	do move alde_alche &rand(99,101) &rand(182,184)
    	do talk &npc(101 184)
    	[
    	log ==============================
    	log =Finalmente virei Alquimista!=
		log ==============================
    	]
    }
}

automacro jaSouAlquimista {
	JobID 18 #Alquimista
	ConfigKey virarAlquimista true
    priority -5
	exclusive 1
	call {
		do conf skillsAddAuto 1
		do conf skillsAddAuto_list AM_AXEMASTERY 10, AM_BIOETHICS 1, AM_REST 1, AM_CALLHOMUN 1, AM_RESURRECTHOMUN 5, AM_LEARNINGPOTION 10, AM_PHARMACY 10, AM_POTIONPITCHER 5, AM_CANNIBALIZE 5, AM_SPHEREMINE 3
		do conf teleportAuto_MaxDmg 1000
		$check = GetIndexAndEquipped("robe", 2560) #Capa Valhalla
		if ($check != -1) do eq $check
		$check = GetIndexAndEquipped("topHead", 5583) #Chapeu valhalla
		if ($check != -1) do eq $check
		call pararDeAtacar
        do conf BetterShopper_on 0
        do conf route_randomWalk_inTown 0
		do conf virarAlquimista none
		do conf questAlquimista none
	}
}